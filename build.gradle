buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven {
        url 'https://plugins.gradle.org/m2/'}
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.3'
        classpath 'net.researchgate:gradle-release:2.4.0'
    }
}

plugins {
    id 'net.researchgate.release' version '2.4.0'
}

apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'groovy'

group = 'com.autoscout24.gradle'

ext.archivesBaseName = 'gradle-monkey-plugin'
ext.isSnapshot = version.endsWith("-SNAPSHOT")

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'com.android.tools.build:gradle:2.2.3'
}

task sourceJar(type: Jar) {
    description = 'An archive of the source code for Maven Central'
    classifier = 'sources'
    from sourceSets.main.groovy
}

task groovydocJar(type: Jar) {
    description = 'An archive of the GroovyDocs for Maven Central'
    classifier = 'javadoc'
    from groovydoc
}

artifacts {
    archives groovydocJar, sourceJar
}

signing {
    sign configurations.archives
}

// Only *require* signing if we are uploading a non snapshot version
gradle.taskGraph.whenReady { taskGraph ->
    tasks.withType(org.gradle.plugins.signing.Sign).all {
        required = taskGraph.hasTask(":uploadArchives") && !isSnapshot
    }
}


install.repositories.mavenInstaller {
    pom.project(pomConfiguration)
    if (signing.signatory) {
        beforeDeployment { signing.signPom(it) }
    }
}

uploadArchives {
    repositories.mavenDeployer {
        if (signing.signatory) {
            beforeDeployment { signPom(it) }
        }
        name = 'mavenCentralReleaseDeployer'
        repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
            authentication(userName: System.properties['sonatypeUsername'], password: System.properties['sonatypePassword'])
            releases(updatePolicy: 'always')
            snapshots(updatePolicy: 'always')
        }
        pom.project(pomConfiguration)
    }
}

release {
    createReleaseTag.dependsOn uploadArchives
}

/**
 * Create POM config and return for use by other tasks.
 */
def getPomConfiguration() {
    return {
        name 'Gradle Monkey Plugin'
        packaging 'jar'
        description 'A Gradle plugin for running Android monkey tests.'
        url 'https://github.com/AutoScout24/gradle-monkey-plugin'
        licenses {
            license {
                name 'The MIT License'
                url 'http://www.opensource.org/licenses/mit-license.php'
                distribution 'repo'
            }
        }
        developers {
            developer {
                id 'scompt'
                name 'Edward Dale'
                email 'scompt@scompt.com'
            }
            developer {
                id 'x2on'
                name 'Felix Schulze'
                email 'code@felixschulze.de'
            }
        }
        scm {
            connection 'scm:https://scompt@github.com/AutoScout24/gradle-monkey-plugin'
            developerConnection 'scm:git@github.com:AutoScout24/gradle-monkey-plugin.git'
            url 'https://github.com/AutoScout24/gradle-monkey-plugin'
        }
    }
}